plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id 'com.google.gms.google-services'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new FileNotFoundException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def prodProperties = new Properties()
def prodPropertiesFile = rootProject.file('prod.properties')
if (prodPropertiesFile.exists()) {
    prodPropertiesFile.withReader('UTF-8') { reader ->
        prodProperties.load(reader)
    }
}

def devProperties = new Properties()
def devPropertiesFile = rootProject.file('dev.properties')
if (devPropertiesFile.exists()) {
    devPropertiesFile.withReader('UTF-8') { reader ->
        devProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.hpodong.hybrid"
    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.hpodong.hybrid"
        minSdkVersion 21
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        prod {
            storeFile file(prodProperties["storeFile"])
            storePassword prodProperties['storePassword']
            keyAlias prodProperties['keyAlias']
            keyPassword prodProperties['keyPassword']
        }
        dev {
            storeFile file(devProperties['storeFile'])
            storePassword devProperties['storePassword']
            keyAlias devProperties['keyAlias']
            keyPassword devProperties['keyPassword']
        }
    }

    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
            manifestPlaceholders = [
                    APP_NAME: prodProperties["app.name"],
                    KAKAO_NATIVE_APP_KEY: prodProperties["kakao.native.app.key"],
                    NAVER_CLIENT_ID: prodProperties["naver.client.id"],
                    NAVER_SECRET: prodProperties["naver.secret"],
                    NAVER_CLIENT_NAME: prodProperties["naver.client.name"],
            ]
            signingConfig signingConfigs.prod
        }
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            manifestPlaceholders = [
                    APP_NAME: devProperties["app.name"],
                    KAKAO_NATIVE_APP_KEY: devProperties["kakao.native.app.key"],
                    NAVER_CLIENT_ID: devProperties["naver.client.id"],
                    NAVER_SECRET: devProperties["naver.secret"],
                    NAVER_CLIENT_NAME: devProperties["naver.client.name"],
            ]
            signingConfig signingConfigs.dev
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            debuggable true
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.2.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-analytics'
}